plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'app.cash.sqldelight'
    alias(libs.plugins.protobuf)
}

android {
    compileSdk buildConfig.compileSdk

    defaultConfig {
        applicationId "io.ubyte.tldr"
        minSdk buildConfig.minSdk
        targetSdk buildConfig.targetSdk

        versionCode buildConfig.version.code
        versionName buildConfig.version.name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            manifestPlaceholders = [ appName:"tldr"]
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            manifestPlaceholders = [ appName:"tldr.debug"]
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        allWarningsAsErrors = false
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.androidxCompose.get()
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    kapt {
        correctErrorTypes true
    }
}

dependencies {
    implementation libs.androidx.activity.compose
    implementation libs.androidx.appcompat
    implementation libs.androidx.compose.material
    implementation libs.androidx.core.ktx
    implementation libs.androidx.datastore.core

    implementation libs.androidx.hilt.navigation.compose
    implementation libs.hilt.android
    kapt libs.hilt.compiler

    implementation libs.ktor.client.core
    implementation libs.ktor.client.okhttp
    implementation libs.logcat
    implementation libs.okio
    implementation libs.protobuf.java.lite
    implementation libs.sqldelight.android.driver
    implementation libs.sqldelight.coroutines

    testImplementation libs.junit4
    androidTestImplementation libs.androidx.compose.ui.test
    androidTestImplementation libs.truth
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.19.4"
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}