plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'app.cash.sqldelight'
    id "com.google.protobuf" version "0.8.18"
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "io.ubyte.tldr"
        minSdk 26
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        allWarningsAsErrors = false
        useIR = true

        freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
        freeCompilerArgs += '-Xopt-in=okio.ExperimentalFileSystem'

        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion rootProject.versions.compose
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    kapt {
        correctErrorTypes true
    }
}

dependencies {
    implementation "androidx.core:core-ktx:$rootProject.versions.core"
    implementation "androidx.appcompat:appcompat:$rootProject.versions.appCompat"
    implementation "com.google.android.material:material:$rootProject.versions.material"

    implementation "androidx.compose.compiler:compiler:$rootProject.versions.compose"
    implementation "androidx.compose.ui:ui:$rootProject.versions.compose"
    implementation "androidx.compose.material:material:$rootProject.versions.compose"
    implementation "androidx.compose.ui:ui-tooling-preview:$rootProject.versions.compose"

    implementation "androidx.activity:activity-compose:$rootProject.versions.activityCompose"

    implementation "androidx.hilt:hilt-navigation-compose:$rootProject.versions.hiltKtx"
    implementation "com.google.dagger:hilt-android:$rootProject.versions.daggerHilt"
    kapt "com.google.dagger:hilt-compiler:$rootProject.versions.daggerHilt"

    implementation  "androidx.datastore:datastore:1.0.0"
    implementation  "com.google.protobuf:protobuf-javalite:3.20.1"

    implementation "io.ktor:ktor-client-core:$rootProject.versions.ktor"
    implementation "io.ktor:ktor-client-okhttp:$rootProject.versions.ktor"

    implementation "app.cash.sqldelight:android-driver:$rootProject.versions.sqldelight"
    implementation "app.cash.sqldelight:coroutines-extensions-jvm:$rootProject.versions.sqldelight"
    implementation "com.squareup.okio:okio:$rootProject.versions.okio"
    implementation "com.squareup.logcat:logcat:$rootProject.versions.logcat"

    testImplementation "com.google.truth:truth:$rootProject.versions.truth"
    androidTestImplementation "com.google.truth:truth:$rootProject.versions.truth"
    testImplementation "junit:junit:$rootProject.versions.junit"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.versions.androidxJunit"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$rootProject.versions.compose"
    debugImplementation "androidx.compose.ui:ui-tooling:$rootProject.versions.compose"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.19.4"
    }
    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}